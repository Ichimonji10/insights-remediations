#!/usr/bin/env groovy

NS='remediations-pr'

def scale (count) {
    sh "oc scale --replicas=${count} --namespace=${NS} dc/postgres"
    sh "oc scale --replicas=${count} --namespace=${NS} dc/playbooks-ssg"

    // wait for dependencies
    waitFor(count, 'postgres')
    waitFor(count, 'playbooks-ssg')
}

def waitFor (count, resource) {
    println "waiting for ${resource} to scale to ${count}"

    openshift.withCluster() {
        openshift.withProject(NS) {
            timeout(2) {
                def latestDeploymentVersion = openshift.selector('dc', resource).object().status.latestVersion
                def rc = openshift.selector('rc', "${resource}-${latestDeploymentVersion}")
                rc.untilEach(1) {
                    def rcMap = it.object()
                    def ready = rcMap.status.readyReplicas == null ? 0 : rcMap.status.readyReplicas
                    return (rcMap.status.replicas.equals(ready))
                }
            }
        }
    }
}

def withScaledEnv(Closure step) {
    lock(NS) {
        scale(1)
        sh "oc get pods --namespace ${NS}"

        try {
            step()
        } finally {
            scale(0)
            sh "oc get pods --namespace ${NS}"
        }
    }
}

node {
    env.NODEJS_HOME = "${tool 'node-10'}"
    env.PATH="${env.NODEJS_HOME}/bin:${env.PATH}"

    checkout scm

    sh 'git rev-parse HEAD'

    stage('build') {
        sh 'npm ci'
    }

    stage('cross-check ssg template validator') {
        sh "node src/validateTemplate.js src/connectors/ssg/mock/standard/*"
    }

    withScaledEnv {

        env.DB_HOST="postgres.${NS}.svc"
        env.DB_DATABASE='remediationstest'

        stage('verify') {
            env.VMAAS_IMPL='mock'

            sh 'npm run verify'
        }

        stage('migration') {
            sh 'npm run db:migrate'
            sh 'npm run db:migrate:undo:all'
        }

        stage('contract tests') {
            env.VMAAS_IMPL='impl'
            env.SSG_IMPL='impl'
            env.SSG_HOST="http://playbooks-ssg.${NS}.svc:8080"

            sh 'npm run test'
        }
    }
}
